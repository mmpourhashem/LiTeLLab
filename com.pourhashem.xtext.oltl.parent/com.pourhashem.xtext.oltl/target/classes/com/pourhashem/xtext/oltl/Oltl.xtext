
/**
 * @author Mohammad Mehdi Pourhashem Kallehbasti
 */
 
grammar com.pourhashem.xtext.oltl.Oltl with org.eclipse.xtext.common.Terminals

generate oltl "http://www.pourhashem.com/xtext/oltl/Oltl"

Model:
	tempFormula=TempFormula
;


Weight:
	(neg='-')? ((wInt=WInt) | (wFloat=WFloat))
;

WInt:
	int=INT
;

WFloat:
	int1=INT '.' int2=INT
//	int1=INT '.' int2=('0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9')* @TODO fix 1.01
;

TempFormula:
	ap=AP
	| ('(' '&&' fa+=TempFormula+ ')' )
	| ('(' '||' fo+=TempFormula+ ')' )
	| ('(' '!!' fnot=TempFormula')' )
	| ('(' op2=OP2 f1=TempFormula f2=TempFormula ')' )
	| ('(' opf=OPF f=TempFormula ')' )
	| ('(' 'w' ap=AP weight=Weight')') //weighted formula
	| ('(' 'next' fNext=TempFormula x1=WInt (x2=WInt)? ')') //Abbreviation
;

terminal OP2: //Propositional operators with 2 operands
	'->' | '<->' | 'until' | 'since' | 'release' | 'trigger'
;

terminal OPF: //Temporal operators with 1 operands
	'next' | 'yesterday' | 'alw' | 'som' | 'alwf' | 'somf' | 'alwp' | 'somp'
;

AP:
	'(' '-' ('P' | 'p') '-' pName=ID ')'
;